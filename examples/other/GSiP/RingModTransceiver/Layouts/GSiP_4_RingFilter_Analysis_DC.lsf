# GSiP_4_RingFilter _Analysis_DC.lsf and .gds
# lsf script for Lumerical INTERCONNECT
# user runs simulation from KLayout to INTERCONNECT
# this script sweeps the DC voltage on the heater
# and plots the spectra


lambda0 = 1546.4e-9; # m, resonance wavelenth

switchtolayout;
select("ONA_1");
set("input parameter","center and range");
set("center frequency",c/lambda0);
set("frequency range",3e11);
set("number of points",5000);
set('peak at maximum',false);


# Heater max amplitude = 1.22
amplitude_values = [0:0.3:1.2];
select("N1");
shiftselect("N3");
shiftselect("N5");
shiftselect("N7");

dat = matrixdataset("dat"); # initialize visualizer data
dat_lambda = matrixdataset("dat_lambda"); # initialize visualizer data
dat_lambda.addparameter("heater voltage^2", amplitude_values^2);
lambda_max = matrix(length(amplitude_values));
for (i=1:length(amplitude_values)) {
    switchtolayout;
    set("amplitude",amplitude_values(i));
    run;
    if (i==1) { 
        T=getresult("ONA_1","input 1/mode 1/transmission");
        wavelength = T.wavelength;
        dat.addparameter("wavelength",wavelength);
    }
    dat.addattribute(num2str(amplitude_values(i)), getattribute( getresult("ONA_1", "input 1/mode 1/gain"), getattribute(getresult("ONA_1", "input 1/mode 1/gain")) ) );    
	lambda_m = getresult("ONA_1","input 1/mode 1/peak/frequency");
	lambda_max(i) = lambda_m(1);
}
dat_lambda.addattribute('peak wavelength', lambda_max);
visualize(dat);
visualize(dat_lambda);

